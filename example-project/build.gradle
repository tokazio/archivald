plugins {
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.ksp)
}

group = 'fr.tokazio.konsistksp'
version = 'undefined'

jar {
    enabled = false
}

repositories {
    mavenCentral()
}

// To compile src/rules independently of the main compilation
// compilation here is not mandatory but it allow to see compilation errors
// before the konsist ksp dynamic compilation step
sourceSets.test.java.srcDirs += ['src/rules']

dependencies {
    // Validate the @ArchitectureRule
    ksp "fr.tokazio.konsistksp:konsistksp-processor:${libs.versions.me.version.get()}"

    // As a testImplementation to use the src/rules
    testImplementation "fr.tokazio.konsistksp:konsistksp-annotations:${libs.versions.me.version.get()}"
    // Use standard-rules
    testImplementation "fr.tokazio.konsistksp:konsistksp-standard-rules:${libs.versions.me.version.get()}"

    testImplementation "io.kotest:kotest-runner-junit5-jvm:${libs.versions.kotest.version.get()}"
}

kotlin {
    jvmToolchain(libs.versions.jvm.version.get().toInteger())
}

ksp {
    arg("konsistKspProjectBase", projectDir.absolutePath)
    arg("konsistKspClasspath", configurations.testCompileClasspath.asPath) // testCompileClasspath to use src/rules
    arg("konsistKspDebug", "false")
    arg("konsistKspFailFast", "false")
    arg("konsistKspIgnoreFailures", "false")
}

test {
    useJUnitPlatform()
}