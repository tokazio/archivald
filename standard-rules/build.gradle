plugins {
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.ksp)
    id 'maven-publish'
}

group = 'fr.tokazio.konsistksp'
version = "${libs.versions.me.version.get()}"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "com.lemonappdev:konsist:${libs.versions.konsist.version.get()}"
    compileOnly "fr.tokazio.konsistksp:annotations"
    ksp "fr.tokazio.konsistksp:processor"
    // Generate META-INF/architecture-rules with @ArchitectureRule

    compileOnly 'javax.inject:javax.inject:1'
    compileOnly 'org.springframework:spring-beans:5.3.31'
}

kotlin {
    jvmToolchain(17)
}

test {
    useJUnitPlatform()
}

ksp {
    arg("konsistKspProjectBase", projectDir.absolutePath)
    arg("konsistKspDebug", "true")
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = 'https://maven.pkg.github.com/tokazio/konsist-ksp'
            credentials {
                username = System.getenv("PACKAGE_USER")
                password = System.getenv("PACKAGE_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}