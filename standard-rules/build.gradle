plugins {
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.ksp)
    id 'maven-publish'
}

group = 'fr.tokazio.konsistksp'
version = "${libs.versions.me.version.get()}"

repositories {
    mavenCentral()
}

dependencies {
    implementation "fr.tokazio.konsistksp:konsistksp-annotations"
    // Generate META-INF/architecture-rules with @ArchitectureRule
    ksp "fr.tokazio.konsistksp:konsistksp-processor"

    implementation "com.lemonappdev:konsist:${libs.versions.konsist.version.get()}"

    implementation 'javax.inject:javax.inject:1'
    implementation 'org.springframework:spring-beans:5.3.31'
}

kotlin {
    jvmToolchain(libs.versions.jvm.version.get().toInteger())
}

ksp {
    arg("konsistKspProjectBase", projectDir.absolutePath)
    arg("konsistKspDebug", "true")
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = 'https://maven.pkg.github.com/tokazio/konsist-ksp'
            credentials {
                username = System.getenv("PACKAGE_USER")
                password = System.getenv("PACKAGE_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
